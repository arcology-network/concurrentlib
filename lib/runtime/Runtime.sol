// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0;

/**
 * @author Arcology Network
 * @title Runtime Library
 * @dev The Runtime Library provides runtime information to developers.
 *      It exposes functions to retrieve pseudo-process IDs (PIDs) and pseudo-random UUIDs.
 */
library Runtime {
    /**
     * @notice Get the pseudo-process ID (PID) from an external contract.
     * @dev The PID is a pseudo-process ID and does not represent a real transaction being processed.
     * @return args The pseudo-process ID (PID) returned by the external contract.
     */
    function pid() internal returns(bytes memory) {
        (,bytes memory random) = address(0xa0).call(abi.encodeWithSignature("pid()"));     
        return random;
    }

    /**
     * @notice Get a pseudo-random UUID from an external contract.
     * @dev The UUID is a pseudo-random number generated by the external contract.
     * @return The pseudo-random UUID returned by the external contract.
     */
    function uuid() internal returns(bytes memory) {
        (,bytes memory id) = address(0xa0).call(abi.encodeWithSignature("uuid()"));     
        return id;
    }
  
    /**
     * @notice Call a custom operation.
     * @return The result of the custom operation.
     */
    function eval(string memory func, bytes memory data) internal returns(bool, bytes memory) {
        return address(0xa0).call(abi.encodeWithSignature(func, data)); 
    }

    /**
     * @notice Set the max parallelism level for a specific function with respect to other functions.
     * @param others The list of function signatures and their contract address that can be executed in parallel.
    */
   function setParallelism(bytes4 func, address addr, bytes4[] memory others, uint64 parallelism) internal returns(bool) {
        (bool success,) = address(0xa0).call(abi.encodeWithSignature("setParallelism(bytes4,address,bytes4[],uint64)", func, addr, others, parallelism));
        return success;
    }
 
    /**
     * @notice Get the number of concurrent instances of the specified function.
     * @return The number of concurrent instances.
     */
    function isInDeferred() internal view returns(bool) {
        (,bytes memory data) = address(0xa0).staticcall(abi.encodeWithSignature("isInDeferred()"));
        return abi.decode(data, (bool));  
    }

    /**
     * @notice Inform the scheduler that a function needs to schedule a defer call. This function can only be called once in the constructor.
     * @return The number of concurrent instances.
     */
    function defer(bytes4 funSign, uint64 prepaidGas) internal returns(bool) {
        (bool successful,) = address(0xa0).call(abi.encodeWithSignature("defer(bytes4,uint64)", funSign, prepaidGas));
        return successful;  
    }
}
